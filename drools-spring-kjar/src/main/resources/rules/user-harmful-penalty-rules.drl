package rules;

import forum.model.Post;
import forum.model.User;
import forum.model.PostLabelEnum;
import forum.model.UserLabelEnum;

import forum.event.DislikePostEvent;
import forum.event.LikePostEvent;
import forum.event.ReportPostEvent;
import forum.event.NewPostEvent;
import forum.event.DefaultUserActionEvent;
import forum.event.LikeQualityPostEvent;
import forum.event.CommunityContributorStreakEvent;
import forum.event.TopUserStreakEvent;

import forum.event.label.TopUserLabelEvent;
import forum.event.label.CommunityContributorUserLabelEvent;
import forum.event.label.SpammerUserLabelEvent;
import forum.event.label.HarmfulUserLabelEvent;
import forum.event.label.TemporarilySuspendedUserLabelEvent;
import forum.event.label.SuspendedUserLabelEvent;

import forum.event.label.TrendingPostLabelEvent;
import forum.event.label.PoorContentPostLabelEvent;
import forum.event.label.PotentiallyHarmfulPostLabelEvent;
import forum.event.label.HarmfulPostLabelEvent;

rule "Harmful user label"
    when        
        $p: Post($uId: postOwnerId, postLabels.contains(PostLabelEnum.HARMFUL))            
        $u: User(username == $uId, !userLabels.contains(UserLabelEnum.HARMFUL_USER))
    then     
    	System.out.println("Harmful user label: " + $uId);
        insert(new HarmfulUserLabelEvent($uId));
    	$u.addLabel(UserLabelEnum.HARMFUL_USER);
    	update($u);        
end

rule "Temorarily suspended user (Spammer)"
    when        
        $u: User($uId: username, userLabels.contains(UserLabelEnum.SPAMMER), !userLabels.contains(UserLabelEnum.TEMPORARILY_SUSPENDED))            
        not $e: SuspendedUserLabelEvent(userId == $uId)
        not	$e1: TemporarilySuspendedUserLabelEvent(userId == $uId)
        	over window:time(2m)
    then      
    	System.out.println("Temorarily suspended user (Spammer): " + $uId);
        insert(new TemporarilySuspendedUserLabelEvent($uId));
    	$u.addLabel(UserLabelEnum.TEMPORARILY_SUSPENDED);
    	update($u);        
end

rule "Revoke temporarily suspended label (Timeout)"
    when        
        $u: User($uId: username, userLabels.contains(UserLabelEnum.TEMPORARILY_SUSPENDED))
        not	$e1: TemporarilySuspendedUserLabelEvent(userId == $uId)
        	over window:time(2m)
    then    
    	System.out.println("Revoke temporarily suspended label (Timeout): " + $uId);       
    	$u.removeLabel(UserLabelEnum.TEMPORARILY_SUSPENDED);
    	update($u);        
end

rule "Suspended user (3x spammer)"
    when        
        $u: User($uId: username, userLabels.contains(UserLabelEnum.SPAMMER), !userLabels.contains(UserLabelEnum.SUSPENDED))            
        not $e1: SuspendedUserLabelEvent(userId == $uId)
        Number(intValue >= 3) from accumulate (
        	$e2: TemporarilySuspendedUserLabelEvent(userId == $uId),
        	count($e2)
        )
    then    
    	System.out.println("Suspended user (3x spammer): " + $uId);  
        insert(new SuspendedUserLabelEvent($uId));
    	$u.addLabel(UserLabelEnum.SUSPENDED);
    	update($u);        
end

rule "Suspended user (3x harmful posts)"
    when        
        $u: User($uId: username, !userLabels.contains(UserLabelEnum.SUSPENDED))   
        Number(intValue >= 3) from accumulate (
        	$p: Post(postOwnerId == $uId, postLabels.contains(PostLabelEnum.HARMFUL)),
        	count($p)
        )
    then      
    	System.out.println("Suspended user (3x harmful posts): " + $uId);
        insert(new SuspendedUserLabelEvent($uId));       
    	$u.addLabel(UserLabelEnum.SUSPENDED);
    	update($u);        
end

rule "Suspended user (Spammer after harmful label)"
    when       
        $e1: HarmfulUserLabelEvent($uId: userId)         
        $e2: SpammerUserLabelEvent(userId == $uId, this after $e1) 
        $u: User(username == $uId, !userLabels.contains(UserLabelEnum.SUSPENDED))   
    then      
    	System.out.println("Suspended user (Spammer after harmful label): " + $uId);
        insert(new SuspendedUserLabelEvent($uId));       
    	$u.addLabel(UserLabelEnum.SUSPENDED);
    	update($u);        
end

rule "Revoke temporarily suspended label (Suspended)"
    when        
        $u: User($uId: username, userLabels.contains(UserLabelEnum.SUSPENDED), userLabels.contains(UserLabelEnum.TEMPORARILY_SUSPENDED))   
    then      
    	System.out.println("Revoke temporarily suspended label (Suspended): " + $uId);     
    	$u.removeLabel(UserLabelEnum.TEMPORARILY_SUSPENDED);
    	update($u);        
end
