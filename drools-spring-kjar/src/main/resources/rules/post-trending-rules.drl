package rules;

import forum.model.Post;
import forum.model.User;
import forum.model.PostLabelEnum;
import forum.model.UserLabelEnum;

import forum.event.DislikePostEvent;
import forum.event.LikePostEvent;
import forum.event.ReportPostEvent;
import forum.event.NewPostEvent;
import forum.event.DefaultUserActionEvent;
import forum.event.LikeQualityPostEvent;
import forum.event.CommunityContributorStreakEvent;
import forum.event.TopUserStreakEvent;

import forum.event.label.TopUserLabelEvent;
import forum.event.label.CommunityContributorUserLabelEvent;
import forum.event.label.SpammerUserLabelEvent;
import forum.event.label.HarmfulUserLabelEvent;
import forum.event.label.TemporarilySuspendedUserLabelEvent;
import forum.event.label.SuspendedUserLabelEvent;

import forum.event.label.TrendingPostLabelEvent;
import forum.event.label.PoorContentPostLabelEvent;
import forum.event.label.PotentiallyHarmfulPostLabelEvent;
import forum.event.label.HarmfulPostLabelEvent;

rule "Trending post detected"
    when
        $post: Post($pId: postId, !postLabels.contains(PostLabelEnum.POOR_CONTENT),  !postLabels.contains(PostLabelEnum.TRENDING), !postLabels.contains(PostLabelEnum.HARMFUL), isTrending(), likes >= 10)
        not $e2: TrendingPostLabelEvent(postId == $pId)
    then    
    	System.out.println("Trending post detected: " + $pId);
    	insert(new TrendingPostLabelEvent($pId));   
    	$post.addLabel(PostLabelEnum.TRENDING); 	    	
        update($post);
end

rule "Trending post label revoked (Timeout)"
    when
        $post: Post($pId: postId, postLabels.contains(PostLabelEnum.TRENDING))        
    	not $e: TrendingPostLabelEvent(postId == $pId)
    			over window:time(5m)
    then
    	System.out.println("Trending post label revoked (Timeout): " + $pId);
        $post.removeLabel(PostLabelEnum.TRENDING);
        update($post);
end

rule "Trending post label revoked (Poor content)"
    when
    	$e: TrendingPostLabelEvent($pId: postId)    	
    	$e2: PoorContentPostLabelEvent(postId == $pId)
        $post: Post(postId == $pId, postLabels.contains(PostLabelEnum.TRENDING))
    then    
    	System.out.println("Trending post label revoked (Poor content): " + $pId);
        delete($e);
        $post.removeLabel(PostLabelEnum.TRENDING);
        update($post);
end

rule "Trending post label revoked (Harmful)"
	salience 9
    when
    	$e: TrendingPostLabelEvent($pId: postId)    	
    	$e2: HarmfulPostLabelEvent(postId == $pId)
        $post: Post(postId == $pId, postLabels.contains(PostLabelEnum.TRENDING))
    then    
    	System.out.println("Trending post label revoked (Harmful): " + $pId);
        delete($e);
        $post.removeLabel(PostLabelEnum.TRENDING);
        update($post);
end